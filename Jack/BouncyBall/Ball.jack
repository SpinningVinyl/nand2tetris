class Ball {

    // a Ball has three properties: center X, center Y and radius
    field int x, y;
    field int r;

    // creates a new Ball object
    constructor Ball new(int initialX, int initialY, int radius) {
        let x = initialX;
        let y = initialY;
        let r = radius;
        // check that we don't go outside of the screen
        if ((r = 0) | (r < 0)) { let r = 1; }
        if ((x - r) < 2) { 
            let x = r + 1; 
        } else { if ((x + r) > 511) {
            let x = 511 - r;
        } }
        if ((y - r) < 2) { 
            let y = r + 1; 
        } else { if ((y + r) > 254) {
            let y = 254 - r;
        } }
        do draw();
        return this;
    }

    // the destructor method
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    // draws the ball on the screen
    method void draw() {
        do Screen.setColor(true);
        do Screen.drawCircle(x, y, r);
        return;
    }

    // erases the ball
    method void erase() {
        do Screen.setColor(false);
        do Screen.drawCircle(x, y, r);
        return;
    }

    // updates the coordinates
    method void setCoordinates(int newX, int newY) {
        let x = newX;
        let y = newY;
        return;
    }


}
